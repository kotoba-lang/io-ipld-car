(ns ipld.car.trustless-test
  (:require #?(:clj [clojure.test :refer [deftest is testing]]
               :cljs [cljs.test :refer [deftest is testing]])
            [ipld.car :as car]
            [ipld.car.bytes :as bytes]
            [ipld.car.trustless :as trustless]
            [ipld.core :as ipld]))

(def limits {:max-blocks 8 :max-bytes 4096 :max-depth 8 :max-matches 8})

(defn fixture []
  (let [store (atom {})
        put! (fn [cid block-bytes] (swap! store assoc cid block-bytes))
        leaf (ipld/put-node! put! {"title" "selected" "null" nil})
        root (ipld/put-node! put! {"child" (ipld/link leaf) "other" true})]
    {:root root :leaf leaf :get-fn #(get @store %)}))

(deftest path-selection-is-a-root-first-verifiable-car
  (let [{:keys [root leaf get-fn]} (fixture)
        result (trustless/path-car get-fn root ["child" "title"] limits)
        parsed (car/decode (get-in result [:car :bytes]))]
    (is (= "selected" (:value result)))
    (is (= trustless/content-type (:content-type result)))
    (is (= [root] (:roots parsed)))
    (is (= [root leaf] (mapv :cid (:entries parsed))))
    (doseq [{:keys [cid]} (:entries parsed)]
      (is (bytes/equal? (get (:blocks parsed) cid) (get-fn cid))))))

(deftest null-is-a-resolved-value-not-a-missing-path
  (let [{:keys [root get-fn]} (fixture)
        result (trustless/path-car get-fn root ["child" "null"] limits)]
    (is (nil? (:value result)))
    (is (= 1 (get-in result [:stats :matches])))))

(deftest missing-path-and-budget-overrun-fail-closed
  (let [{:keys [root get-fn]} (fixture)]
    (testing "missing path is not presented as an absence proof"
      (is (thrown? #?(:clj Exception :cljs js/Error)
                   (trustless/path-car get-fn root ["missing"] limits))))
    (testing "the root and linked leaf cannot exceed the block budget"
      (is (thrown? #?(:clj Exception :cljs js/Error)
                   (trustless/path-car get-fn root ["child" "title"]
                                       (assoc limits :max-blocks 1)))))))
